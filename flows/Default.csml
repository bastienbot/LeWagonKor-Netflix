start:
	say "Hello, I'm your Netflix assistant !"
	goto what_type

what_type:
	say Question("What are you looking for ?",
	buttons=[
		Button("Movie ğŸ¬") as btnmovie,
		Button("Series ğŸ‘®â€â™‚ï¸") as btnserie,
		Button("Anything ğŸ˜") as btnany
		])
	hold
	if (event match btnmovie) {
		remember type = "movie"
	}
	else if (event match btnserie) {
		remember type = "serie"
	} else {
    remember type = "any"
  }
// ############################################################
// Now it's your turn, fill in the missing part below
// ############################################################


// Before starting, you need to remove the line 27 below

  goto what_genre




what_genre:
  // 1 - Write the question that the chatbot will ask prior showing all genres
  say Question("What genre of movies or shows do you like ?",
	buttons=[
    Button("Action & Adventure"),
    Button("Animation"),
    Button("Documentary Films"),
    Button("Dramas"),
    Button("Family Comedies"),
    Button("Horror Movies"),
    Button("Music & Musicals"),
    Button("Sci-Fi & Fantasy"),
    Button("Thrillers")
		])
    // 2 - Now the chatbot needs to wait for the user input, what's the keyword?
    hold
    // 3 - Once the user has answered, the chatbot needs ro remember what the user has said
    //     by creating a new variable `genre`, a keyword is missing
    remember genre = event
    goto what_year
  
what_year:
  say Question("Are you looking for a specific year of release ?",
	buttons=[
    // 4 - We need to show a button "No"
    // 5 - This button needs to be assigned to the variable `btnNo`
		Button("No ğŸ™…â€â™€ï¸") as btnNo
		])
  hold
  if (event match btnNo) {
    remember year = "any"
  } else {
    if (event.is_int() && event >= 1950 && event <= 2020) {
      remember year = event
    } else {
      say "This is not a valid date, you need to write a date as a 4 digits number, between 1950 and 2020."
      goto what_year
    }
  }
  // 6 - The chatbot now needs to go to the `is_sorted` step, write the instruction below
  goto is_sorted


is_sorted:
  // 7 - Ask the user if he wants the results sorted by date
  //     The question will show two buttons :
  //     - "Yes" that will be assigned to btnYes
  //     - "No" that will be assigned to btnNo
  say Question("Would you like the resulted sorted by date (new movies first ğŸ˜‰)",
	buttons=[
		Button("Yes âœ…") as btnYes,
    Button("No ğŸ¥´") as btnNo
		])
  // 8 - Again, the chatbot needs to wait for the user input
  hold
  // 9 - if the user has clicked "Yes", create a variable `sort` set to `true`
  //     otherwise, `sort` will be set to `false`
  if (event match btnYes) {
    remember sort = true
  } else {
    remember sort = false
  }
  // 10 - Final step, we need to go to the `display` step
  goto display

display:
  // 11 - Now, we call the function, attaching all the known variables, fill in the blanks below
  do res = Fn("Netflix", genre=genre, type=type, sort=sort, year=year)
  do cards = []
  if (res.length() == 0) {
    say "No movies are matching your requirements, sorry ğŸ˜”"
  }
  foreach (val, index) in res {
    if (val.avgrating) do title = "{{val.avgrating}}/10 - {{val.title}}"
    else do title = val.title
    do newCard = Card(
      title=title,
      subtitle=val.synopsis,
      image_url=val.img,
      buttons=[Button("Find out more", payload=val.title)]
    )
    if (index < 6) do cards.push(newCard)
  }
  say Carousel(
    cards = cards
  )
  goto moreInfos

moreInfos:
  hold
  if (event == "Check other movies") goto start
  do url = Fn("urlEncode", url="https://www.netflix.com/search?q={{event}}")
  say Url(url, text="{{url}} on Netflix")
  say Button("Check other movies")
  goto moreInfos
